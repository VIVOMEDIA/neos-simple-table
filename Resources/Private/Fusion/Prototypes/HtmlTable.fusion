prototype(VIVOMEDIA.SimpleTable:HtmlTable) < prototype(Neos.Fusion:Tag) {

  tagName = 'table'
  attributes.class = Neos.Fusion:RawArray
  @context.data = ${[[]]}

  content = Neos.Fusion:RawArray {
    tableHead = VIVOMEDIA.SimpleTable:HtmlTableHead {
      @context.rows = ${[Array.first(data)]}

      @if.hasTableHead = ${q(node).property('hasTableHead') && Type.isArray(data)}
    }
    tableBody = VIVOMEDIA.SimpleTable:HtmlTableBody {
      @context.hasHightlightRow = ${q(node).property('hasHighlightColumn')}
      @context.rows = ${q(node).property('hasTableHead') ? Array.shift(data) : data}
    }
  }
  content.@process.combine = ${Array.join(value, '')}
}

prototype(VIVOMEDIA.SimpleTable:HtmlTableHead) < prototype(Neos.Fusion:Tag) {
  tagName = 'thead'
  @context.rows = ${rows}
  attributes.class = Neos.Fusion:RawArray

  content = Neos.Fusion:Collection {
    @context.isHeadRow = true
    collection = ${rows}
    itemRenderer = VIVOMEDIA.SimpleTable:HtmlTableRow
  }
}


prototype(VIVOMEDIA.SimpleTable:HtmlTableBody) < prototype(Neos.Fusion:Tag) {
  tagName = 'tbody'
  attributes.class = Neos.Fusion:RawArray

  content = Neos.Fusion:Collection {
    collection = ${rows}
    itemRenderer = VIVOMEDIA.SimpleTable:HtmlTableRow
  }
}

prototype(VIVOMEDIA.SimpleTable:HtmlTableRow) < prototype(Neos.Fusion:Tag) {
  tagName = 'tr'
  attributes.class = Neos.Fusion:RawArray

  content = Neos.Fusion:Collection {
    collection = ${item}
    iterationName = 'iterator'
    itemRenderer = Neos.Fusion:Case {
      highlight {
        condition = ${(hasHightlightRow && iterator.index == 0) || isHeadRow}
        renderer = VIVOMEDIA.SimpleTable:HtmlTableHighlightColumn
      }
      default {
        condition = ${true}
        renderer = VIVOMEDIA.SimpleTable:HtmlTableColumn
      }
    }
  }
}

prototype(VIVOMEDIA.SimpleTable:HtmlTableColumn) < prototype(Neos.Fusion:Tag) {
  tagName = 'td'
  content = Neos.Fusion:Case {
    default {
      condition = true
      renderer = ${item}
      @position = 'end'
    }
  }

  attributes.class = Neos.Fusion:RawArray
}


prototype(VIVOMEDIA.SimpleTable:HtmlTableHighlightColumn) < prototype(VIVOMEDIA.SimpleTable:HtmlTableColumn) {
  tagName = 'th'
  content = ${item}
}
