prototype(VIVOMEDIA.SimpleTable:HtmlTable) < prototype(Neos.Fusion:Tag) {
  tagName = 'table'
  attributes.class = Neos.Fusion:RawArray

  data = ${[[]]}
  caption = null
  @context {
    data = ${this.data}
    caption = ${this.caption}
  }

  content = Neos.Fusion:Array {
    caption = VIVOMEDIA.SimpleTable:HtmlTableCaption {
        @if.hasCaption = ${caption ? true : false}
        @context.caption = ${caption}
    }
    tableHead = VIVOMEDIA.SimpleTable:HtmlTableHead {
      @context.rows = ${[Array.first(data)]}

      @if.hasTableHead = ${q(node).property('hasTableHead') && Type.isArray(data)}
    }
    tableBody = VIVOMEDIA.SimpleTable:HtmlTableBody {
      @context {
        hasHightlightRow = ${q(node).property('hasHighlightColumn')}
        rows = ${q(node).property('hasTableHead') ? Array.shift(data) : data}
      }
    }
  }
}

prototype(VIVOMEDIA.SimpleTable:HtmlTableCaption) < prototype(Neos.Fusion:Tag) {
	tagName = 'caption'
	content = ${caption}
}

prototype(VIVOMEDIA.SimpleTable:HtmlTableHead) < prototype(Neos.Fusion:Tag) {
  tagName = 'thead'
  attributes.class = Neos.Fusion:RawArray

  @context.isHeadRow = true

  content = Neos.Fusion:Collection {
    collection = ${rows}
    itemRenderer = VIVOMEDIA.SimpleTable:HtmlTableRow
  }
}


prototype(VIVOMEDIA.SimpleTable:HtmlTableBody) < prototype(Neos.Fusion:Tag) {
  tagName = 'tbody'
  attributes.class = Neos.Fusion:RawArray

  content = Neos.Fusion:Collection {
    collection = ${rows}
    itemRenderer = VIVOMEDIA.SimpleTable:HtmlTableRow
  }
}

prototype(VIVOMEDIA.SimpleTable:HtmlTableRow) < prototype(Neos.Fusion:Tag) {
  tagName = 'tr'
  attributes.class = Neos.Fusion:RawArray

  content = Neos.Fusion:Collection {
    collection = ${item}
    iterationName = 'iterator'
    itemRenderer = Neos.Fusion:Case {
      highlight {
        condition = ${(hasHightlightRow && iterator.index == 0) || isHeadRow}
        renderer = VIVOMEDIA.SimpleTable:HtmlTableHighlightColumn
      }
      default {
        condition = ${true}
        renderer = VIVOMEDIA.SimpleTable:HtmlTableColumn
      }
    }
  }
}

prototype(VIVOMEDIA.SimpleTable:HtmlTableColumn) < prototype(Neos.Fusion:Tag) {
  tagName = 'td'
  content = Neos.Fusion:Case {
    default {
      condition = true
      renderer = ${item}
      @position = 'end'
    }
  }

  attributes.class = Neos.Fusion:RawArray
}

prototype(VIVOMEDIA.SimpleTable:HtmlTableHighlightColumn) < prototype(VIVOMEDIA.SimpleTable:HtmlTableColumn) {
  tagName = 'th'
}
